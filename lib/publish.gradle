apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.1-SNAPSHOT'

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        Production(MavenPublication) {
            groupId 'cc.appauto.nglib'
            artifactId 'lib'
            version this.version

            artifact("$buildDir/outputs/aar/lib-release.aar")
            /*
            artifact("$buildDir/intermediates/aar_main_jar/release/classes.jar") {
                classifier = 'sources'
            }
            */

            artifact androidSourcesJar

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url("file://"+project.rootDir.absolutePath+"/repository")
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user =  properties.getProperty('bintrayUser', null) ?: System.getenv('BINTRAY_USER')
    key = properties.getProperty('bintrayApiKey', null) ?: System.getenv('BINTRAY_API_KEY')

    publications = ['Production']
    override = true
    pkg {
        userOrg = 'appautong'
        repo = 'appautong'
        name = 'lib'
        description = "appautong library"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/appautong/lib.git'
        dryRun = false
        version {
            name = this.version
            desc = "appautong lib ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}
